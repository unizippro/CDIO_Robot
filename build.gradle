plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

group 'cdio'
version '1.0-SNAPSHOT'

def opencvProperties = new Properties()
opencvProperties.load(new FileInputStream(rootProject.file("opencv.properties")))

repositories {
    mavenCentral()
}

dependencies {
    implementation project("robot")
    implementation("commons-collections:commons-collections:3.2")
    implementation("com.github.bdeneuter:lejos-ev3-api:0.9.1-beta")
    implementation("com.google.inject:guice:4.0")

    // OpenCV
    implementation(fileTree(dir: opencvProperties["libDir"], includes: ["*.jar"]))
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.encoding = "UTF-8"
}

javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' , 'javafx.swing' ]
}

run {
    mainClassName = "group14.Application"
    systemProperty "app.demo", project.property("app.demo")
    systemProperty "java.library.path", opencvProperties["libDir"]
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation("com.github.bdeneuter:lejos-ev3-api:0.9.1-beta")
    }
}

